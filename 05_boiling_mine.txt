// potion: lucky or berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var bardiche_using = 0
var hammer_using = 0
var wand_using = 0
var boss_motion_count = 0

func boss_attack()
  follow.ignition(0)
  // 開始待ち
  ?time < 540 | foe.state = -1
    move.speed_walk()
  :?item.GetCooldown("blade") = 0 
    weapon.equip_weapon(0)
    activate R
  :?item.GetCooldown("mask") = 0
    weapon.equip_weapon(1)
    activate R
  :?foe.state = 32
    boss_motion_count++
    ?boss_motion_count > 9
      weapon.equip_weapon(30)
  // 近接距離
  :?pos.y > 0 & foe.distance >= 6
    weapon.equip_weapon(31)
  :?foe.distance <= 5
    ?item.GetCooldown("hammer") = 0 | buffs.string = bardiche
      weapon.equip_weapon(5)
      activate R
    :?foe.armor > 0
      ?time%17 = 0
        ?hammer_using = 15
          weapon.equip_weapon(16)
          hammer_using = 16
        :
          weapon.equip_weapon(15)
          hammer_using = 15
    :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
      weapon.equip_weapon(3)
      activate R
    :
      ?time%20 = 0
        ?bardiche_using = 3
          weapon.equip_weapon(4)
          bardiche_using = 4
        :
          weapon.equip_weapon(3)
          bardiche_using = 3
  :
    activate potion
    ?time%16 = 0
      ?wand_using = 25
        weapon.equip_weapon(26)
        wand_using = 26
      :
        weapon.equip_weapon(25)
        wand_using = 25
    boss_motion_count = 0

func normal_attack()
  ?foe.count >= 10 & item.GetCooldown("blade") = 0
    weapon.equip_weapon(0)
    activate R
  // ☆11 ボンバーカート
  :?foe = bomb_cart
    ?foe.distance <= 6
      weapon.equip_weapon(30)
    :
      weapon.equip_weapon(7)
  // 連射2発で倒せるなら遠距離から始末
  :?foe.hp + foe.armor <= 30
    weapon.equip_weapon(7)
  :?foe.distance > 10
    ?item.GetCooldown("bash") <= 0
      weapon.equip_weapon(9)
    :
      weapon.equip_weapon(10)
  :?(foe.count >= 10 & item.GetCooldown("hammer") = 0) | buffs.string = bardiche
    weapon.equip_weapon(5)
    activate R
  :
    weapon.equip_weapon(5)

/** 地獄の鉱山 **/
// ボス部屋判定可能
?loc = bronze_guardian
  boss_attack()
:?loc = bronze_mine
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 20
    move.walk()
  :
    normal_attack()
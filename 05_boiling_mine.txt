// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var boss_motion_count = 0
var heavy_hammer_motion = 0
var heavy_hammer_flip = false
var hammer_flip = false
var wand_flip = false
var sword_flip = false

func boss_attack()
  follow.ignition(0)
  // 開始待ち
  ?time < 540 | foe.state = -1
    move.speed_walk()
  :?item.GetCooldown("blade") = 0 
    weapon._equip(0, false)
    activate R
  :?item.GetCooldown("mask") = 0
    weapon._equip(11, false)
    activate R
  :?foe.state = 32
    boss_motion_count++
    ?boss_motion_count > 9
      weapon._equip(53, false)
  // 近接距離
  :?pos.y > 0
    // トリスケル + ダッシュで距離を詰める
    ?foe.distance >= 6
      weapon._equip(54, false)
    :?item.GetCooldown("hammer") = 0 | buffs.string = bardiche
      weapon._equip(4, false)
      activate R
    :?foe.armor > 0
      ?time%17 = 0
        hammer_flip = !hammer_flip
        weapon._equip(30, hammer_flip)
    :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
        weapon._equip(2, false)
        activate R
    // todo: 近距離から遠距離への移行時、ワンドで殴りたい
    :
      ?time%12 = 0
        sword_flip = !sword_flip
        weapon._equip(22, sword_flip)
  :
    ?time%16 = 0
      wand_flip = !wand_flip
      weapon._equip(42, wand_flip)
      activate potion
    boss_motion_count = 0

func normal_attack()
  ?item.GetCooldown("blade") = 0 & foe.count >= 10
    weapon._equip(0, false)
    activate R
  // 連射2発で倒せる且つ敵の数が少ないなら遠距離から始末
  :?foe.hp + foe.armor <= 36 & foe.count <= 3
    weapon._equip(6, false)
  :?foe.distance > 6
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
    heavy_hammer_motion = 0
  // ☆11 ボンバーカート
  :?foe = bomb_cart & foe.distance < 7
    weapon._equip(53, false)
  :?(foe.count >= 10 & item.GetCooldown("hammer") = 0) | buffs.string = bardiche
    weapon._equip(4, false)
    activate R
    heavy_hammer_motion = 0
    heavy_hammer_flip = false
  :
    // todo: heavy hammer 持ち替え？モーションキャンセルのロスは被ダメの影響が大きいので考慮必要
    ?heavy_hammer_motion%20 = 0
      ?heavy_hammer_flip
        weapon._equip(4, false)
      :
        weapon._equip(5, false)
      heavy_hammer_flip = !heavy_hammer_flip
    heavy_hammer_motion++

/** 地獄の鉱山 **/
// ボス部屋判定可能
?loc = bronze_guardian
  boss_attack()
:?loc = bronze_mine
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 20
    move.action()
  :
    normal_attack()
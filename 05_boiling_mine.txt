// potion: crit

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var boss_motion_count = 0
var wand_flip = false

func boss_attack()
  follow.ignition(300)
  // 開始待ち
  ?time < 540 | foe.state = -1
    move.speed_walk()
  :?item.GetCooldown("blade") = 0 
    weapon._equip(0, false)
    activate R
  :?item.GetCooldown("mask") = 0
    weapon._equip(11, false)
    activate R
  // 回避ロジック
  :?foe.state = 32
    boss_motion_count++
    ?boss_motion_count > 5
      weapon._equip(53, false)
  // 近接距離
  :?pos.y > 0
    // トリスケル + ダッシュで距離を詰める
    ?foe.distance >= 6
      ?item.GetCooldown("dash") <= 0
        weapon._equip(54, false)
      :
        weapon._equip(55, false)
    :?foe.armor > 0
      ?item.GetCooldown("hammer") <= 0 | buffs.string = bardiche
        weapon._equip(4, false)
        activate R
      :
        // todo: aether2本持ち必要、斧はいらないかな？
        weapon._equip(24, false)
        activate potion
    :?item.GetCooldown("bardiche") <= 0 | buffs.string = bardiche
        weapon._equip(6, false)
        activate R
    // todo: 近距離から遠距離への移行時、ワンドで殴りたい
    :
      // todo: aether2本持ち必要
      weapon._equip(24, false)
  :
    ?time%8 = 0
      wand_flip = !wand_flip
      weapon._equip(28, wand_flip)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") <= 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 11
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  // ☆11 ボンバーカート
  :?foe = bomb_cart & foe.distance < 7
    weapon._equip(53, false)
  :
    // todo: 1週目かつsmite無しの場合、ice小杖は効率が悪い…が1面の敵が5体以上のケースが多いので考慮不要か？
    weapon._equip(25, wand_flip)

/** 地獄の鉱山 **/
// ボス部屋判定可能
?loc = bronze_guardian
  boss_attack()
:?loc = bronze_mine
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 20
    move.action()
  :
    normal_attack()
// potion: ???

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// initialize
move.set_harvest_pick(false)

// 変数
var bearer_wand_focus = false
var bearer_wand_motion_count = 0
var bearer_chill_focus = 0

var elementalist_sword_using = 0

var perfected_attack_motion_count = 0
var perfected_hammer_using = 0
var perfected_chill_focus = 0
var perfected_sword_using = 0

func boss_attack()
  follow.ignition(0)
  ?foe = bearer
    state1()
  :?foe = elementalist
    state2()
  :?foe = perfected
    state3()
  :
    move.walk()

// 第一形態
func state1()
  ?foe.distance >= 6
    weapon.equip_weapon(10)
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon.equip_weapon(3)
    activate R
  :?item.GetCooldown("blade") = 0
    weapon.equip_weapon(0)
    activate R
  :?item.GetCooldown("mask") = 0
    weapon.equip_weapon(1)
    activate R
  :?item.GetCooldown("hammer") <= 0 | buffs.string = bardiche
    weapon.equip_weapon(5)
    activate R
  // todo: sword で付与した方がいいかも
  :?(time%180 = 0 | bearer_wand_focus) & bearer_wand_motion_count <= 35
    weapon.equip_weapon(23)
    bearer_wand_focus = true
    bearer_wand_motion_count++
  :?foe.debuffs.string ! dot
    weapon.equip_weapon(29)
  :?(foe.debuffs.string ! chill | bearer_chill_focus) & foe.debuffs.string ! "chill:6"
    weapon.equip_weapon(19)
    bearer_chill_focus = true
    bearer_wand_focus = false
    bearer_wand_motion_count = 0
  :
    weapon.equip_weapon(21)
    bearer_wand_focus = false
    bearer_chill_focus = false
    bearer_wand_motion_count = 0

// 第二形態
func state2()
  ?foe.distance >= 6
    weapon.equip_weapon(10)
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon.equip_weapon(3)
    activate R
  :?buffs.count = 0
    weapon.equip_weapon(32)
    activate potion
  :
    ?time%12 = 0
      ?elementalist_sword_using = 19
        weapon.equip_weapon(21)
        elementalist_sword_using = 21
      :
        weapon.equip_weapon(19)
        elementalist_sword_using = 19
  // todo: 目の表示取得できたので弱点武器を担ぐ処理を追加可能
  var symbol = draw.GetSymbol(54, 9)
  >`10,10,week = @symbol@

// 第三形態
func state3()
  // 光線回避
  ?foe.state = 115
    perfected_attack_motion_count++
    ?perfected_attack_motion_count >= 60
      weapon.equip_weapon(30)
  :
    perfected_attack_motion_count = 0
    ?foe.distance >= 9
      weapon.equip_weapon(10)
    :?foe.armor > 0
      ?time%17 = 0
        ?perfected_hammer_using = 17
          weapon.equip_weapon(18)
          perfected_hammer_using = 18
        :
          weapon.equip_weapon(17)
          perfected_hammer_using = 17
    :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
      weapon.equip_weapon(3)
      activate R
    :?item.GetCooldown("blade") = 0
      weapon.equip_weapon(0)
      activate R
    :?item.GetCooldown("mask") = 0
      weapon.equip_weapon(1)
      activate R
    :?hp < 10
      weapon.equip_weapon(14)
    :?(foe.debuffs.string ! chill | perfected_chill_focus) & foe.debuffs.string ! "chill:6"
      weapon.equip_weapon(19)
      perfected_chill_focus = true
    :
      perfected_chill_focus = false
      ?time%12 = 0
        ?perfected_sword_using = 21
          weapon.equip_weapon(22)
          perfected_sword_using = 22
        :
          weapon.equip_weapon(21)
          perfected_sword_using = 21
    
// todo: アルカディアの偵察兵
func normal_attack()
  var a = 0

/** ロッキー高原 **/
?loc = rocky_plateau
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 40
    move.walk()
  :?foe = acronian_scout
    normal_attack()
  :
    boss_attack()
// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// initialize
move.set_harvest_pick(false)

// 変数
var weapon_flip = false
var perfected_attack_motion_count = 0
var perfected_attack_state32_count = 0
var perfected_attack_state32_flg = false

func boss_attack()
  ?foe = bearer
    state1()
  :?foe = elementalist
    state2()
  :?foe = perfected
    state3()
  :
    move.action()

// 第一形態
func state1()
  ?foe.distance >= 6
    weapon._equip(52, false)
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon._equip(2, false)
    activate R
  :?item.GetCooldown("blade") = 0
    weapon._equip(0, false)
    activate R
  :?item.GetCooldown("mask") = 0
    weapon._equip(11, false)
    activate R
  // poison, chill は付与し続ける
  :?foe.debuffs.string ! "damage"
    weapon._equip(30, false)
  :?foe.debuffs.string ! "chill:6"
    weapon._equip(25, false)
  :?time%12 = 0
    weapon_flip = !weapon_flip
    weapon._equip(23, weapon_flip)

// 第二形態
func state2()
  // 目の表示を取得
  // ∞ ♥ * φ ❄ 
  var symbol = draw.GetSymbol(54, 9)
  >`10,10,week = @symbol@

  ?foe.distance > 6
    weapon._equip(52, false)
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon._equip(2, false)
    activate R
  // 弱点属性の判別
  :?time%12 = 0
    var weapon_index = 23
    ?symbol = ∞
      weapon_index = 24
    :?symbol = ♥
      weapon_index = 20
    :?symbol = "*"
      weapon_index = 21
    :?symbol = φ
      weapon_index = 22
    :?symbol = ❄
      weapon_index = 23
    weapon_flip = !weapon_flip
    weapon._equip(weapon_index, weapon_flip)

// 第三形態
func state3()
  follow.ignition(0)
  // 光線回避
  ?foe.state = 115
    perfected_attack_state32_count = 0
    perfected_attack_motion_count++
    ?perfected_attack_motion_count >= 60
      ?item.GetCooldown("mind") <= 0
        weapon._equip(53, false)
      :
        weapon._equip(43, false)
  :
    perfected_attack_motion_count = 0
    ?foe.state = 32 & !perfected_attack_state32_flg
      perfected_attack_state32_count++
      perfected_attack_state32_flg = true
    :?foe.state ! 32
      perfected_attack_state32_flg = false

    ?foe.state = 32 & perfected_attack_state32_count = 3
      weapon._equip(53, false)
    :
      weapon._equip(43, false)
    ?foe.distance >= 9
      weapon._equip(52, false)
    :?foe.armor > 0
      ?item.GetCooldown("hammer") = 0 | buffs.string = bardiche
        weapon._equip(4, false)
        activate R
      :?time%17 = 0
        weapon_flip = !weapon_flip
        weapon._equip(29, weapon_flip)
    :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
      weapon._equip(2, false)
      activate R
    :?item.GetCooldown("blade") = 0
      weapon._equip(0, false)
      activate R
    :?item.GetCooldown("mask") = 0
      weapon._equip(11, false)
      activate R
    // poison, chill は付与し続ける
    :?foe.state = 32 & foe.debuffs.string ! "damage"
      weapon._equip(30, false)
    :?foe.debuffs.string ! "chill:6"
      weapon._equip(25, false)
    :
      ?time%12 = 0
        weapon_flip = !weapon_flip
        weapon._equip(23, weapon_flip)
        activate potion
    
// アルカディアの偵察兵はスタンハメ可能
func normal_attack()
  ?foe.distance > 7
    weapon._equip(51, false)
  :?time%20 = 0
    ?weapon_flip
      weapon._equip(4, false)
    :
      weapon._equip(5, false)
    weapon_flip = !weapon_flip

/** ロッキー高原 **/
?loc = rocky_plateau
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 40
    move.action()
  :?foe = acronian_scout
    normal_attack()
  :
    boss_attack()
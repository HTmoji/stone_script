// potion: crit

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// initialize
var harvest_pick = false
move.set_harvest_pick(harvest_pick)

var weapon_flip = true

func boss_attack()
  ?foe = bearer
    state1()
  :?foe = elementalist
    state2()
  :?foe = perfected
    state3()
  :
    move.action()

// 第一形態
func state1()
  // 変身前にhammerで先制攻撃可能
  ?foe.distance <= 24 & foe.distance > 18 & (item.GetCooldown("hammer") = 0 | buffs.string = bardiche)
    weapon._equip(4, false)
    activate R
  // 戦闘前の待機
  :?foe.state = 100 | foe.state = 101 | foe.state = 102 | foe.state = 117 | foe.state = 118 | foe.state = 126 | foe.state = 127 | foe.hp <= 0
    move.action()
  :?foe.distance > 11
    weapon._equip(52, false)
  :?item.GetCooldown("mask") = 0
    weapon._equip(11, false)
    activate R
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon._equip(6, false)
    activate R
  :
    var third_hand = 44
    ?foe.debuffs.string ! "damage"
      third_hand = 35
    :?foe.debuffs.string ! "chill:6"
      third_hand = 32
    ?time%3 = 0
      weapon._equip(42, false)
    :?time%3 = 1
      weapon._equip(43, false)
    :?time%3 = 2
      weapon._equip(third_hand, false)

// 第二形態
func state2()
  ?foe.state = 107 | foe.hp <= 0
    move.action()
  :?foe.distance > 11
    weapon._equip(52, false)
  :
    // 弱点属性の判別
    // todo: AACするなら最低でも属性毎D剣が2本必要なので両手殴りでお茶を濁す
    ?foe = Poison
      ?time%3 = 0
        weapon._equip(30, false)
      :?time%3 = 1
        weapon._equip(31, false)
      :?time%3 = 2
        weapon._equip(32, false)
    :?foe = Vigor
      ?time%3 = 0
        weapon._equip(33, false)
      :?time%3 = 1
        weapon._equip(34, false)
      :?time%3 = 2
        weapon._equip(35, false)
    :?foe = AEther
      ?time%3 = 0
        weapon._equip(36, false)
      :?time%3 = 1
        weapon._equip(38, false)
      :?time%3 = 2
        weapon._equip(21, true)
    :?foe = Fire
      ?time%3 = 0
        weapon._equip(39, false)
      :?time%3 = 1
        weapon._equip(40, false)
      :?time%3 = 2
        weapon._equip(22, false)
    :?foe = Ice
      ?time%3 = 0
        weapon._equip(42, false)
      :?time%3 = 1
        weapon._equip(43, false)
      :?time%3 = 2
        weapon._equip(44, false)
    :
      var third_hand = 44
      ?foe.debuffs.string ! "damage"
        third_hand = 35
      :?foe.debuffs.string ! "chill:6"
        third_hand = 32
      :?hp < maxhp
        third_hand = 38
      ?time%3 = 0
        weapon._equip(42, false)
      :?time%3 = 1
        weapon._equip(43, false)
      :?time%3 = 2
        weapon._equip(third_hand, false)

// 第三形態
func state3()
  follow.ignition(500)

  ?foe.armor > 0 & hp > 0
    loc.Leave()
  :?foe.distance >= 7
    weapon._equip(52, false)
  :
    var third_hand = 44
    ?foe.debuffs.string ! "damage"
      third_hand = 35
    :?foe.debuffs.string ! "chill:6"
      third_hand = 32
    :?hp < maxhp
      third_hand = 38
    :
      activate potion
    ?time%3 = 0
      weapon._equip(42, false)
    :?time%3 = 1
      weapon._equip(43, false)
    :?time%3 = 2
      weapon._equip(third_hand, false)

// アルカディアの偵察兵
func normal_attack()
  ?foe.distance > 16
    move.speed_walk()
  :?foe.distance > 7
    weapon._equip(51, false)
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon._equip(6, false)
    activate R
  // moon AAC todo: 炎デバフで時短も考慮
  :?time%3 = 0
    weapon._equip(32, false)
  :?time%3 = 1
    weapon._equip(35, false)
  :?time%3 = 2
    weapon._equip(38, false)

/** ロッキー高原 **/
?loc = rocky_plateau
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  // x座標でボス位置判定している、☆11以上前提(220)、それ以下なら120とか
  :?foe.count = 0 & pos.x < 220
    move.action()
  :?foe = acronian_scout | pos.x < 120
    normal_attack()
  :
    ?harvest_pick
      loc.Leave()
    :
      boss_attack()
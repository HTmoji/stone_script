// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var attack_motion = 0
var heavy_hammer_flip = true
var bardiche_flip = false
var sword_flip = false

func boss_attack()
  ?foe = yeti
    follow.ignition(1000)
  ?foe.armor > 0
    ?foe.distance >= 6
      weapon._equip(52, false)
    :
      weapon._equip(29, false)
  :?item.GetCooldown("blade") = 0
    weapon._equip(0, false)
    activate R
  :?item.GetCooldown("mask") = 0
    weapon._equip(11, false)
    activate R
  :?foe = yeti & foe.distance <= 7 & (item.GetCooldown("bardiche") = 0 | buffs.string = bardiche)
    weapon._equip(2, false)
    activate R
  // 謎の待機時間
  :?foe = yeti & foe.state = 1
    move.action()
  // ノックバックモーション
  // todo: fire long wand とか持たせる？
  :?foe.state = 133
    weapon._equip(43, false)
  // ノックバックモーション終了後は即距離を詰める
  :?foe.distance >= 8
    weapon._equip(52, false)
  // 登場モーション終了後はchill を重ねる
  :?foe.state = 132 & foe.debuffs.string ! "chill:6"
      weapon._equip(25, false)
      activate potion
  :
    // todo: buff, debuff に応じてfire sword の持ち替えできるが…そこまでするか？
    weapon._equip(23, false)

func normal_attack()
  ?item.GetCooldown("blade") = 0 & foe.count >= 10
    weapon._equip(0, false)
    activate R
  :?foe.distance >= 6 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
    attack_motion = 0
  // heavy hammer を使うよりも早く処理できる場合
  :?foe.hp < 100 & foe.count < 3
    weapon._equip(9, false)
  // ☆11 ガリガリさん、ツララ
  :?foe = Giant Ice Elemental
    attack_motion = 0
    ?time%12 = 0
      sword_flip = !sword_flip
      weapon._equip(23, sword_flip)
  :?foe = ice_pillar
    ?item.GetCooldown("hammer") = 0 | buffs.string = bardiche
      weapon._equip(4, false)
      activate R
      attack_motion = 0
      heavy_hammer_flip = false
    :
      ?attack_motion%20 = 0
        ?bardiche_flip
          weapon._equip(2, bardiche_flip)
        :
          weapon._equip(3, bardiche_flip)
        bardiche_flip = !bardiche_flip
      attack_motion++
  // 処理漏れでchill を貰った際はheavy hammerでごり押し
  :?debuffs.count > 0
    weapon._equip(4, false)
    activate R
  :
    ?attack_motion%20 = 0
      ?heavy_hammer_flip
        weapon._equip(4, heavy_hammer_flip)
      :
        weapon._equip(5, heavy_hammer_flip)
      heavy_hammer_flip = !heavy_hammer_flip
    attack_motion++

/** 氷の尾根 **/
// ボス部屋判定不可
?loc = icy_ridge
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 20
    move.action()
    attack_motion = 0
  :?foe = yeti | foe = ice_wall | pos.x > 500
    boss_attack()
  :
    normal_attack()
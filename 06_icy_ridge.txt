// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var sword_flip = false

func boss_attack()
  ?foe = yeti
    follow.ignition(1000)
  ?foe.armor > 0
    ?foe.distance >= 6
      weapon._equip(52, false)
    :
      weapon._equip(29, false)
  :?item.GetCooldown("blade") = 0
    weapon._equip(0, false)
    activate R
  :?item.GetCooldown("mask") = 0
    weapon._equip(11, false)
    activate R
  :?foe = yeti & foe.distance <= 7 & (item.GetCooldown("bardiche") = 0 | buffs.string = bardiche)
    weapon._equip(6, false)
    activate R
  // 謎の待機時間
  :?foe = yeti & foe.state = 1
    move.action()
  // 待機後の距離詰め
  :?foe.distance >= 8
    weapon._equip(52, false)
  // smite chill attack
  :?foe.state = 132 & foe.debuffs.string ! "chill:6"
      weapon._equip(24, false)
      activate potion
  :
    // chill詰んだ後はfireでトドメ
    weapon._equip(23, false)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") = 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 10
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  // ☆11 ガリガリさん
  :?foe = Giant Ice Elemental
    // todo: flipしてもいいかも
    weapon._equip(23, sword_flip)
  // ツララ
  :?foe = ice_pillar
    ?item.GetCooldown("hammer") = 0 | buffs.string = bardiche
      weapon._equip(4, false)
      activate R
    :?foe.debuffs.string ! "chill:6"
      weapon._equip(40, wand_flip)
    :
      // todo: chill6積んだあとのつららの処理はどうするか要検討
      weapon._equip(40, wand_flip)
  :
    // todo: 1週目かつsmite無しの場合、ice小杖は効率が悪い…が1面の敵が5体以上のケースが多いので考慮不要か？
    weapon._equip(40, wand_flip)

/** 氷の尾根 **/
// ボス部屋判定不可
?loc = icy_ridge
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 20
    move.action()
  :?foe = yeti | foe = ice_wall | pos.x > 500
    boss_attack()
  :
    normal_attack()
// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var bardiche_using = 0
var heavy_hammer_using = 0
var attack_motion = 0

func boss_attack()
  ?foe = yeti
    follow.ignition(0)
  ?foe.armor > 0
    ?foe.distance >= 6
      weapon.equip_weapon(10)
    :
      weapon.equip_weapon(17)
  :?item.GetCooldown("blade") = 0
    weapon.equip_weapon(0)
    activate R
  :?item.GetCooldown("mask") = 0
    weapon.equip_weapon(1)
    activate R
  :?foe = yeti & foe.distance <= 7 & (item.GetCooldown("bardiche") = 0 | buffs.string = bardiche)
    weapon.equip_weapon(3)
    activate R
  // 謎の待機時間
  :?foe = yeti & foe.state = 1
    move.walk()
  // todo: もうちょっといい武器を持たせたい
  :?foe.state = 133
    weapon.equip_weapon(29)
  :?foe.distance >= 8
    weapon.equip_weapon(10)
  // 登場モーション終了後
  :?foe.state = 132 & foe.debuffs.string ! "chill:6"
      weapon.equip_weapon(19)
      activate potion
  :
    // todo: buff, debuff に応じてfire sword の持ち替え判定をする必要がある
    weapon.equip_weapon(21)

func normal_attack()
  ?foe.count >= 10 & item.GetCooldown("blade") = 0
    weapon.equip_weapon(0)
    activate R
  :?foe.distance >= 6
    ?item.GetCooldown("bash") <= 0
      weapon.equip_weapon(9)
    :
      weapon.equip_weapon(10)
    attack_motion = 0
  // ☆11 ガリガリさん、ツララ
  :?foe = Giant Ice Elemental | foe = ice_pillar
    ?item.GetCooldown("hammer") = 0 | buffs.string = bardiche
      weapon.equip_weapon(5)
      activate R
      attack_motion = 0
    :
      ?attack_motion%20 = 0
        ?bardiche_using = 3
          weapon.equip_weapon(4)
          bardiche_using = 4
        :
          weapon.equip_weapon(3)
          bardiche_using = 3
      attack_motion++

  // 処理漏れでchill を貰った際はheavy hammerでごり押し
  :?debuffs.count > 0
    weapon.equip_weapon(6)
    activate R
  :
    ?attack_motion%19 = 0
      ?heavy_hammer_using = 5
        weapon.equip_weapon(6)
        heavy_hammer_using = 6
      :
        weapon.equip_weapon(5)
        heavy_hammer_using = 5
    attack_motion++

/** 氷の尾根 **/
// ボス部屋判定不可
?loc = icy_ridge
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 22
    move.walk()
    attack_motion = 0
  :?foe = yeti | foe = ice_wall | pos.x > 500
    boss_attack()
  :
    normal_attack()
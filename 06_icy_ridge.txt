// potion: crit

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var pause = false

func boss_attack()
  // 距離詰め
  ?foe.distance >= 8
    weapon._equip(52, false)
  // アーマー剥がし
  :?foe.armor > 0 & (item.GetCooldown("hammer") <= 0 | buffs.string = bardiche)
    weapon._equip(4, false)
    activate R
    activate potion
    pause = true
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon._equip(6, false)
    activate R
    activate potion
  // チートくさい動き
  :?foe = yeti & foe.state = 1 & pause
    ui.OpenInv()
    pause = false
  // moon AAC
  :
    ?time%3 = 0
      weapon._equip(42, false)
    :?time%3 = 1
      weapon._equip(43, false)
    :?time%3 = 2
      weapon._equip(32, false)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") <= 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 10
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  // ☆11 ガリガリさん
  :?foe = Giant Ice Elemental
      // moon AAC
      ?time%3 = 0
        weapon._equip(42, false)
      :?time%3 = 1
        weapon._equip(43, false)
      :?time%3 = 2
        weapon._equip(35, false)
  // ツララ
  :?foe = ice_pillar
    ?foe.debuffs.string ! "chill:6"
      weapon._equip(25, wand_flip)
    :
      // moon AAC
      // todo: 月時計側の攻撃で足が止まることがあるので処理方法は別がいいかも
      ?time%3 = 0
        weapon._equip(42, false)
      :?time%3 = 1
        weapon._equip(43, false)
      :?time%3 = 2
        weapon._equip(35, false)
    // dUで消す方法は微妙かも
    // weapon._equip(27, wand_flip)
  :
    // todo: 1週目かつsmite無しの場合、ice小杖は効率が悪い…が1面の敵が5体以上のケースが多いので考慮不要か？
    weapon._equip(25, wand_flip)

/** 氷の尾根 **/
// ボス部屋判定不可
?loc = icy_ridge
  ?hp < maxhp
    loc.Leave()
  :?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 20
    move.action()
  :?foe = yeti | foe = ice_wall | pos.x > 500
    boss_attack()
  :
    normal_attack()
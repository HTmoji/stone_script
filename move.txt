var harvest_pick = false

func set_harvest_pick(b)
  harvest_pick = b

func walk()
  // todo: activate Eが効かないのでゲーム開始から1F待機して判定
  ?summon.count = 0 & time > 1
    activate_summon()
  :?harvest_pick & harvest.distance <= 7
      harvest_walk()
  :?pickup & pickup.distance <= 7
      pick_walk()
  :?hp = maxhp
    speed_walk()
  :
    heal_walk()

func activate_summon()
  equipR fire talisman
  ?item.CanActivate("fire_talisman")
    activate R

func harvest_walk()
  ?harvest = Boulder
    equip shovel
  :
    // todo: hatchet のactive を振りたい
    equipL triskelion stone
    equipR hatchet

func pick_walk()
  equipL triskelion stone
  equipR star_stone

func heal_walk()
  equip quarterstaff
  ?(time > 10 & item.CanActivate() & item.GetCooldown("quarterstaff") = 0) | buffs.string = quarterstaff
    activate R
  :
    equipL ouroboros
    equipR com shield

func speed_walk()
  equip quarterstaff
  ?(time > 10 & item.CanActivate() & item.GetCooldown("quarterstaff") = 0) | buffs.string = quarterstaff
    activate R
  :
    equipL triskelion stone
    equipR com shield
// external script
var move = import user_script/move
var weapon = import user_script/weapon

// 変数
var wand_using = 0
var bardiche_using = 0

func boss_attack()
  // 第一形態
  ?foe = boss_harder
    // 待ち時間
    ?time < 400
      move.walk()
    :?foe.distance > 5 
      weapon.equip_weapon(0)
    :?foe.distance <= 7 & foe.hp < 620
      weapon.equip_weapon(1)
      activate R
    // デバフは常に付与
    :?foe.debuffs.string ! dot | foe.debuffs.string ! damage
      weapon.equip_weapon(19)
    :?foe.debuffs.string ! chill
      weapon.equip_weapon(9)
    :
      ?time%20 = 0
        ?bardiche_using = 2
          weapon.equip_weapon(2)
          bardiche_using = 3
        :
          weapon.equip_weapon(1)
          bardiche_using = 2
  // モブ処理
  :?foe.count >= 2
    ?time%10 = 0
      ?wand_using = 0
        weapon.equip_weapon(8)
        wand_using = 9
      :
        weapon.equip_weapon(9)
        wand_using = 0
  // 第二形態
  :?foe = boss_stage_2
    ?item.GetCooldown("mask") = 0
      weapon.equip_weapon(14)
      activate R
    :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
      weapon.equip_weapon(1)
      ?foe.distance <= 7
        activate R
      :
        weapon.equip_weapon(0)
    // 岩投げ以外は連射
    :?foe.state = 32 | foe.state = 33
      weapon.equip_weapon(19)
    :
      weapon.equip_weapon(10)
  :
    move.speed_walk()

func normal_attack()
  ?foe.count >= 10 & item.GetCooldown("blade") = 0
    weapon.equip_weapon(11)
    activate R
  // ☆11 墓対策
  :?pos.x > 440 & foe.distance >= 10
    weapon.equip_weapon(0)
  :
    ?time%10 = 0
      ?wand_using = 9
        weapon.equip_weapon(8)
        wand_using = 8
      :
        weapon.equip_weapon(9)
        wand_using = 9

/** 呪われた会館 **/
// ボス部屋判定可能
?loc = undead_crypt_boss
  boss_attack()
  ?hp <= 20
    activate potion
:?loc = undead_crypt
  ?foe.distance > 17
    move.walk()
  :
    normal_attack()
  ?hp <= 20
    activate potion
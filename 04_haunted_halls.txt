// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var wand_flip = false
var sword_flip = false

func boss_attack()
  // 第一形態
  ?foe = boss_harder
    // モーション中に削られる分を考慮
    follow.ignition(1000)
    // 待ち時間
    ?time < 400
      move.action()
    :?foe.distance > 5
      weapon._equip(52, false)
    :?foe.hp < 745
      weapon._equip(2, false)
      activate R
    :
      ?time%12 = 0
        sword_flip = !sword_flip
        weapon._equip(21, sword_flip)
  // 第二形態
  :?foe = boss_stage_2
    ?foe.distance > 8
      weapon._equip(52, false)
    :
      ?time%12 = 0
        sword_flip = !sword_flip
        weapon._equip(21, sword_flip)
        activate potion
  // 雑魚処理、ただし召喚中後は位置関係でボスを殴るのでボス処理に実行される
  :
    ?time%16 = 0
      wand_flip = !wand_flip
      weapon._equip(41, wand_flip)

func normal_attack()
  ?item.GetCooldown("blade") = 0 & foe.count >= 10
    weapon._equip(0, false)
    activate R
  // ☆11 墓対策
  // todo: 時短したいが☆15だと削りきられるので対策必要
  // todo: ワンパンできないことに起因している
  :?foe.distance >= 10 & pos.x > 440
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  :
    ?time%16 = 0
      wand_flip = !wand_flip
      weapon._equip(41, wand_flip)

/** 呪われた会館 **/
// ボス部屋判定可能
?loc = undead_crypt_boss
  ?foe.distance > 22
    move.action()
  :
    boss_attack()
:?loc = undead_crypt
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance > 17
    move.action()
  :
    normal_attack()
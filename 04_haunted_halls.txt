// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var sword_flip = false

func boss_attack()
  // 第一形態
  ?foe = boss_harder
    follow.ignition(600)
    // 待ち時間
    ?time < 400 | foe.distance > 10
      move.action()
    :?item.GetCooldown("mask") = 0
      weapon._equip(11, false)
      activate R
    :?foe.hp < 1000 & foe.distance <= 7 & (item.GetCooldown("bardiche") = 0 | buffs.string = bardiche)
      weapon._equip(6, false)
      activate R
    :?foe.debuffs.string ! "chill:6"
      weapon._equip(24, false)
    :?foe.debuffs.string ! "damage"
      weapon._equip(20, false)
    :
      // todo: flip?
      weapon._equip(21, false)
  // 第二形態
  :?foe = boss_stage_2
    ?foe.distance > 11
      weapon._equip(52, false)
    :?foe.debuffs.string ! "chill:6"
      weapon._equip(24, false)
      activate potion
    :?foe.debuffs.string ! "damage"
      weapon._equip(20, false)
    :
      // todo: flip?
      weapon._equip(21, false)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") = 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 10
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // 墓石bardiche
  :?pos.x > 485 & (item.GetCooldown("bardiche") = 0 | buffs.string = bardiche)
    weapon._equip(6, false)
    activate R
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  :
    // todo: 1週目かつsmite無しの場合、ice小杖は効率が悪いのでvigor小杖を持ちたい
    weapon._equip(40, false)

/** 呪われた会館 **/
// ボス部屋判定可能
?loc = undead_crypt_boss
  ?foe.distance > 22
    move.action()
  :
    boss_attack()
:?loc = undead_crypt
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance > 16
    move.action()
  :
    normal_attack()
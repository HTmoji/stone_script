// potion: crit

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var harder_alive = true

func boss_attack()
  // 第一形態
  ?foe = boss_harder
    harder_alive = false
    ?item.GetCooldown("mask") <= 0
      weapon._equip(11, false)
      activate R
    :?item.GetCooldown("hammer") <= 0 | buffs.string = bardiche
      weapon._equip(4, false)
      activate R
    :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
      ?item.GetCooldown("bash") <= 0
        weapon._equip(51, false)
      :
        weapon._equip(52, false)
    :
      ?time%3 = 0
        weapon._equip(32, false)
      :?time%3 = 1
        weapon._equip(33, false)
      :?time%3 = 2
        weapon._equip(34, false)
  // 第二形態 手前/奥
  :?foe = boss_fat | foe = boss_skinny
    harder_alive = true
    ?foe.distance >= 7 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
      ?item.GetCooldown("bash") <= 0
        weapon._equip(51, false)
      :
        weapon._equip(52, false)
    :
      ?time%3 = 0
        weapon._equip(32, false)
        activate potion
      :?time%3 = 1
        weapon._equip(33, false)
      :?time%3 = 2
        weapon._equip(34, false)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") <= 0 & foe.count >= 1
    screen.Next()
    ?foe.distance < 11
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // ☆11のカタツムリは適度に距離を保ってbardiche
  :?foe.armor >= 100 & pos.x > 400
    weapon._equip(6, false)
    ?foe.distance <= 13
      activate R
  // 毛玉はdash(bash) で距離を詰めた後、1Fでmind を挟むことで回避可能
  :?foe = fluff & foe.distance <= 5 & item.GetCooldown("mind") = 0
    weapon._equip(53, false)
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  :
    weapon._equip(25, wand_flip)

/** キノコの森 **/
// ボス部屋判定可能
?loc = fungus_forest_boss
  ?foe.distance > 17 & harder_alive
    move.action()
  :
    boss_attack()
:?loc = fungus_forest
  harder_alive = true
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 20
    move.action()
  :
    normal_attack()
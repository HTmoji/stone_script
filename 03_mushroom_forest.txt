// potion: crit

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var harder_alive = true

func boss_attack()
  // 第一形態
  ?foe = boss_harder
    harder_alive = false
    ?item.GetCooldown("blade") = 0
      weapon._equip(0, false)
      activate R
    :?item.GetCooldown("mask") = 0
      weapon._equip(11, false)
      activate R
    :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
      ?item.GetCooldown("bash") <= 0
        weapon._equip(51, false)
      :
        weapon._equip(52, false)
    // smite chill attack
    :?foe.debuffs.string ! "chill:6"
      weapon._equip(24, false)
    :
      weapon._equip(20, false)
  // 第二形態 手前
  :?foe = boss_fat
    harder_alive = true
    ?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
      weapon._equip(6, false)
      activate R
    // smite chill attack
    :?foe.debuffs.string ! "chill:6"
      weapon._equip(24, false)
      activate potion
    :
      weapon._equip(20, false)
  // 第二形態 奥
  :?foe = boss_skinny
    ?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
      ?item.GetCooldown("bash") <= 0
        weapon._equip(51, false)
      :
        weapon._equip(52, false)
    :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
      weapon._equip(6, false)
      activate R
    // smite chill attack
    :?foe.debuffs.string ! "chill:6"
      weapon._equip(24, false)
    :
      weapon._equip(20, false)
  :
    weapon._equip(10, false)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") <= 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 11
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // ☆11のカタツムリは適度に距離を保ってbardiche
  :?foe.armor >= 100 & pos.x > 350
    weapon._equip(6, false)
    ?foe.distance <= 9
      activate R
  // 毛玉はdash(bash) で距離を詰めた後、1Fでmind を挟むことで回避可能
  :?foe = fluff & foe.distance <= 5 & item.GetCooldown("mind") = 0
    weapon._equip(53, false)
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  :
    // todo: 1週目かつsmite無しの場合、ice小杖は効率が悪い…が1面の敵が5体以上のケースが多いので考慮不要か？
    weapon._equip(25, wand_flip)

/** キノコの森 **/
// ボス部屋判定可能
?loc = fungus_forest_boss
  ?foe.distance > 17 & harder_alive
    move.action()
  :
    boss_attack()
:?loc = fungus_forest
  harder_alive = true
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 20
    move.action()
  :
    normal_attack()
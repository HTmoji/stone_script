// external script
var move = import user_script/move
var weapon = import user_script/weapon

// initialize
move.set_harvest_pick(false)

func boss_attack()
  // 第二形態 
  ?foe = poena
    ?(foe.hp < 620 & item.GetCooldown("bardiche") = 0) | buffs.string = bardiche 
      weapon.equip_weapon(1)
      activate R
    :?foe.distance >= 7
      weapon.equip_weapon(0)
    :?foe.state = 32
      ?foe.debuffs.count <= 0
        weapon.equip_weapon(15)
      :
        weapon.equip_weapon(16)
    :
      weapon.equip_weapon(11)
  // 第一形態
  :?foe = boss
    ?item.GetCooldown("blade") = 0
      weapon.equip_weapon(11)
      activate R
    :?item.GetCooldown("mask") = 0
      weapon.equip_weapon(14)
      activate R
    :?(foe.hp < 620 & item.GetCooldown("bardiche") = 0) | buffs.string = bardiche
      weapon.equip_weapon(1)
      activate R
    :?foe.distance >= 7
      weapon.equip_weapon(13)
    :
      weapon.equip_weapon(5)

func normal_attack()
  ?item.GetCooldown("blade") = 0 & foe.count >= 10
    weapon.equip_weapon(11)
    activate R
  ?foe ! scarab
    weapon.equip_weapon(10)
  :
    // 石虫: bardiche で削り切れなければ剣
    ?foe.hp <= 72
      weapon.equip_weapon(5)
    :
      weapon.equip_weapon(2)

/** 枯木の渓谷 **/
?loc = deadwood_valley
  ?foe.distance >= 22
    move.walk()
  :?foe = boss | foe = poena
    boss_attack()
  :?foe ! boss
    normal_attack()
  :
    move.walk()
  ?hp <= 25
      activate potion
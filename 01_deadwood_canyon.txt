// potion: ???

// external script
var move = import user_script/move
var weapon = import user_script/weapon

// 変数
var bardiche_using = 0
var sword_using = 0
var poena_flg = false

// initialize
move.set_harvest_pick(false)

func boss_attack()
  // 第二形態 
  ?foe = poena
    ?(foe.hp < 720 & item.GetCooldown("bardiche") = 0) | buffs.string = bardiche 
      weapon.equip_weapon(3)
      activate R
    :?foe.distance >= 7
      weapon.equip_weapon(10)
      poena_flg = false
    :?foe.buffs.string ! mirror | poena_flg
      poena_flg = true
      ?foe.debuffs.count < 6
        weapon.equip_weapon(19)
        activate potion
      :
        // todo: 弱点ないので高火力かオノのstunハメで削り切る
        ?time%17 = 0
          ?sword_using = 22
            weapon.equip_weapon(21)
            sword_using = 21
          :
            weapon.equip_weapon(22)
            sword_using = 22
    :
      weapon.equip_weapon(0)
  // 第一形態
  :?foe = boss
    ?item.GetCooldown("blade") = 0
      weapon.equip_weapon(0)
      activate R
    :?item.GetCooldown("mask") = 0
      weapon.equip_weapon(1)
      activate R
    :?(foe.hp < 720 & item.GetCooldown("bardiche") = 0) | buffs.string = bardiche
      weapon.equip_weapon(3)
      activate R
    :?foe.distance >= 7
      weapon.equip_weapon(9)
    :?time%20 = 0
      ?bardiche_using = 3
        weapon.equip_weapon(4)
        bardiche_using = 4
      :
        weapon.equip_weapon(3)
        bardiche_using = 3

func normal_attack()
  ?item.GetCooldown("blade") = 0 & foe.count >= 10
    weapon.equip_weapon(0)
    activate R
  ?foe ! scarab
    weapon.equip_weapon(7)
  :?foe.distance >= 8
    ?item.GetCooldown("bash") <= 0
      weapon.equip_weapon(9)
    :
      weapon.equip_weapon(10)
  :?item.GetCooldown("hammer") <= 0 | buffs.string = bardiche
    weapon.equip_weapon(5)
    activate R
  :
    weapon.equip_weapon(5)

/** 枯木の渓谷 **/
?loc = deadwood_valley
  ?foe.distance >= 22
    move.walk()
  :?foe = boss | foe = poena
    boss_attack()
  :?foe ! boss
    normal_attack()
  :
    move.walk()
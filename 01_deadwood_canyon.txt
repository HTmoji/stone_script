// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon

// 変数
var poena_flg = false

// initialize
move.set_harvest_pick(false)

func boss_attack()
  // 第二形態 
  ?foe = poena
    // 距離詰め
    ?foe.distance >= 11 &  item.GetCooldown("dash") <= 0
        weapon._equip(52, false)
    :?foe.state = 1
      weapon._equip(0, false)
    :?foe.buffs.string ! mirror | poena_flg & state = 32
      weapon._equip(24, false)
      activate potion
    :
      weapon._equip(0, false)
  // 第一形態
  :?foe = boss
    ?item.GetCooldown("blade") = 0
      weapon._equip(0, false)
      activate R
    :?item.GetCooldown("mask") = 0
      weapon._equip(11, false)
      activate R
    // 距離詰め
    :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
      ?item.GetCooldown("bash") <= 0
        weapon._equip(51, false)
      :
        weapon._equip(52, false)
    :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
      weapon._equip(6, false)
      activate R
    :
      weapon._equip(24, false)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") <= 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 11
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  :?foe ! scarab
    weapon._equip(40, false)
  :
    // todo: smiteあるので速度エンチャのheavy hammerがほしい
    weapon._equip(4, false)

/** 枯木の渓谷 **/
?loc = deadwood_valley
  ?foe.distance >= 18
    move.action()
  :?foe = boss | foe = poena
    boss_attack()
  :?foe ! boss
    normal_attack()
  :
    move.action()
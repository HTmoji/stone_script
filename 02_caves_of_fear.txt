// potion: crit

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

func boss_attack()
  ?foe.hp > 0
    ?item.GetCooldown("mask") = 0
      weapon._equip(11, false)
      activate R
    :
      ?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
        ?item.GetCooldown("bash") <= 0
          weapon._equip(51, false)
        :
          weapon._equip(52, false)
      :
        ?time%3 = 0
          weapon._equip(30, false)
          activate potion
        :?time%3 = 1
          weapon._equip(31, false)
        :?time%3 = 2
          weapon._equip(32, false)
  :
    move.action()


func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") <= 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 11
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  // ☆11~ コウモリーズ
  :?foe = boss
    ?time%3 = 0
      weapon._equip(30, false)
    :?time%3 = 1
      weapon._equip(31, false)
    :?time%3 = 2
      weapon._equip(32, false)
  :
    weapon._equip(25, false)

/** 恐怖の洞窟 **/
// ボス部屋判定不可 且つ 距離判定不可
?loc = caves
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
    // initialize
    move.set_shield("ice socketed_shield")
  :?foe.distance >= 20
    move.action()
  :?foe ! Bolesh
    normal_attack()
  :?foe = Bolesh
    boss_attack()
  :
    move.action()
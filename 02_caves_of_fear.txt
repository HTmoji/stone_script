// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var attack_count = 0
var wand_motion_count = 0
var debuff_using = 0
var wand_using = 0

func boss_attack()
  follow.ignition(720)
  ?(foe.hp < 620 & item.GetCooldown("bardiche") = 0) | buffs.string = bardiche
    ?foe.distance >= 8
      weapon.equip_weapon(10)
    :
      weapon.equip_weapon(3)
      activate R
  // damage debuffは切らさない
  :?(attack_count%85 = 0 | debuff_using = 1) & wand_motion_count < 16
    weapon.equip_weapon(29)
    debuff_using = 1
    wand_motion_count++
  :
    activate potion
    debuff_using = 0
    wand_motion_count = 0
    ?attack_count%16 = 0
      ?wand_using = 23
        weapon.equip_weapon(24)
        wand_using = 24
      :
        weapon.equip_weapon(23)
        wand_using = 23
  attack_count++

func normal_attack()
  ?item.GetCooldown("blade") = 0 & foe.count > 5
    weapon.equip_weapon(0)
    activate R
    attack_count = 0
  :?foe.distance >= 8
    ?item.GetCooldown("bash") <= 0
      weapon.equip_weapon(9)
    :
      weapon.equip_weapon(10)
    attack_count = 0
  // ☆11 コウモリーズ
  :?foe = boss & foe.hp < 720
      weapon.equip_weapon(3)
      activate R
      attack_count = 0
  :
    ?attack_count%16 = 0
      ?wand_using = 23
        weapon.equip_weapon(24)
        wand_using = 24
      :
        weapon.equip_weapon(23)
        wand_using = 23
    attack_count++

/** 恐怖の洞窟 **/
// ボス部屋判定不可 且つ 距離判定不可
?loc = caves
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance >= 22
    move.walk()
    attack_count = 0
  :?foe ! Bolesh
    normal_attack()
  :?foe = Bolesh
      boss_attack()
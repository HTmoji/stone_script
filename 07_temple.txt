// potion: crit

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

func boss_attack()
  follow.ignition(0)
  // 毒吐きモーション、捕食モーション、等考慮しなくてもよくなった…
  ?(foe.state = 103 | foe.state = 104 | foe.state = 105) & debuffs.count >= 20
    weapon._equip(1, false)
  // 落石
  :?foe.state = 112
      weapon._equip(53, false)
  // 距離詰め
  :? foe.distance > 7
    weapon._equip(51, false)
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon._equip(6, false)
    activate R
  // moon AAC
  :
    ?time%3 = 0
      weapon._equip(30, false)
    :?time%3 = 1
      weapon._equip(32, false)
    :?time%3 = 2
      weapon._equip(35, false)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") = 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 10
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  // ☆11の特殊敵 時間経過でバフ
  :?foe = acronian_cultist
    ?foe.hp < 1000
      weapon._equip(6, false)
      activate R
    // moon AAC
    :
      ?time%3 = 0
        weapon._equip(30, false)
      :?time%3 = 1
        weapon._equip(32, false)
      :?time%3 = 2
        weapon._equip(35, false)
  :
    // smite attack
    weapon._equip(25, wand_flip)

/** 神殿 **/
// ボス部屋判定可能
?loc = nagaraja
  ?foe.distance > 17
    move.action()
  :
    boss_attack()
:?loc = temple
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance > 17
    move.action()
  :
    normal_attack()
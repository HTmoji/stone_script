// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var heavy_hammer_using = 0
var sword_using = 0

func boss_attack()
  follow.ignition()
  // 捕食モーション
  ?foe.state = 103 | foe.state = 104 | foe.state = 105
    ?debuffs.count >= 20
      weapon.equip_weapon(4)
    :
      weapon.equip_weapon(19)
  // 落石
  :?foe.state = 112
      weapon.equip_weapon(30)
  // 毒吐きモーション
  // todo: デバフ蓄積されているなら6を握るべき？
  :?foe.state = 32 | foe.state = 33
    ?foe.debuffs.string ! dot
      weapon.equip_weapon(12)
    :
      weapon.equip_weapon(13)
  :?debuffs.count >= 20
    weapon.equip_weapon(4)
    activate R
  :
    ?time%12 = 0
      ?sword_using = 19
        weapon.equip_weapon(20)
        sword_using = 20
      :
        weapon.equip_weapon(19)
        sword_using = 19

func normal_attack()
  ?item.GetCooldown("blade") = 0 & foe.count >= 10
    weapon.equip_weapon(0)
    activate R
  // ☆11の特殊敵 時間経過でバフ
  :?foe = acronian_cultist
    ?foe.hp < 620
      weapon.equip_weapon(3)
      activate R
    :?foe.distance > 6
      weapon.equip_weapon(9)
    :?foe.debuffs.string ! damage
      weapon.equip_weapon(29)
    :
      ?time%19 = 0
        ?heavy_hammer_using = 5
          weapon.equip_weapon(6)
          heavy_hammer_using = 6
        :
          weapon.equip_weapon(5)
          heavy_hammer_using = 5
  // 一部mobは近接で仕留める
  :?foe = cult_guard | foe = worm_rider
    ?foe.hp < 107
      weapon.equip_weapon(4)
    :
      weapon.equip_weapon(0)
  :
    weapon.equip_weapon(23)

/** 神殿 **/
// ボス部屋判定可能
?loc = nagaraja
  ?foe.distance > 17
    move.walk()
  :
    boss_attack()
  ?hp <= 5 | debuffs.count >= 25
    activate potion
:?loc = temple
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance > 17
    move.walk()
  :
    normal_attack()
// potion: berserk

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 変数
var heavy_hammer_flip = 0
var sword_flip = 0

func boss_attack()
  follow.ignition(0)
  // 毒吐きモーション、捕食モーション、等考慮しなくてもよくなった…
  ?(foe.state = 103 | foe.state = 104 | foe.state = 105) & debuffs.count >= 20
    weapon._equip(3, false)
  // 落石
  :?foe.state = 112
      weapon._equip(53, false)
  // 距離詰め
  :? foe.distance > 7
    weapon._equip(51, false)
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon._equip(2, false)
    activate R
  // デバフは切らさない
  :?foe.debuffs.string ! "chill:6"
    weapon._equip(25, false)
  :
    ?time%12 = 0
      sword_flip = !sword_flip
      weapon._equip(24, sword_flip)
      activate potion

func normal_attack()
  ?item.GetCooldown("blade") = 0 & foe.count >= 15
    weapon._equip(0, false)
    activate R
  // ☆11の特殊敵 時間経過でバフ
  :?foe = acronian_cultist
    ?foe.hp < 745
      weapon._equip(2, false)
      activate R
    :?foe.distance > 6
      weapon._equip(51, false)
    :?foe.debuffs.string ! "chill:6"
      weapon._equip(25, false)
    :
      ?time%19 = 0
        ?heavy_hammer_flip
          weapon._equip(4, heavy_hammer_flip)
        :
          weapon._equip(5, heavy_hammer_flip)
        heavy_hammer_flip = !heavy_hammer_flip
  // 一部mobは近接で仕留める
  :?foe = cult_guard | foe = worm_rider
    ?foe.hp < 113
      ?foe.distance > 7
        ?item.GetCooldown("bash") <= 0
          weapon._equip(51, false)
        :
          weapon._equip(52, false)
      :
        weapon._equip(4, false)
        ?(foe.count >= 5 & item.GetCooldown("hammer") = 0) | buffs.string = bardiche
          activate R
    :
      weapon._equip(0, false)
  // todo: デバフ積みすぎて連射クロスボウがゴミになった際の条件分岐を入れるべき
  :
    weapon._equip(9, false)

/** 神殿 **/
// ボス部屋判定可能
?loc = nagaraja
  ?foe.distance > 17
    move.action()
  :
    boss_attack()
:?loc = temple
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
  :?foe.distance > 17
    move.action()
  :
    normal_attack()
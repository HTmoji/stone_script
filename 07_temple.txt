// potion: crit

// external script
var move = import user_script/move
var weapon = import user_script/weapon
var follow = import user_script/follow

// 落石判定
var count = 0

func boss_attack()
  follow.ignition(0)
  // ポーションはデバフ攻撃のタイミングで起動
  ?foe.state = 32
    activate potion
  // 落石回避後
  ?foe.state = 112 & foe.distance > 4
    weapon._equip(25, false)
  // 落石
  :?foe.state = 112
    count++
    ?count > 30
      weapon._equip(53, false)
  // 距離詰め
  :?foe.distance > 7
    weapon._equip(51, false)
  :?item.GetCooldown("bardiche") = 0 | buffs.string = bardiche
    weapon._equip(6, false)
    activate R
  // moon AAC
  :
    count = 0
    //weapon._equip(16, false)

    ?time%3 = 0
      weapon._equip(30, false)
    :?time%3 = 1
      weapon._equip(31, false)
    :?time%3 = 2
      weapon._equip(32, false)

func normal_attack()
  // 2画面blade
  ?item.GetCooldown("blade") = 0 & foe.count >= 5
    screen.Next()
    ?foe.distance < 10
      weapon._equip(0, false)
      activate R
      screen.Resetoffset()
  // 距離詰め
  :?foe.distance >= 11 & (item.GetCooldown("bash") <= 0 | item.GetCooldown("dash") <= 0)
    ?item.GetCooldown("bash") <= 0
      weapon._equip(51, false)
    :
      weapon._equip(52, false)
  // ☆11の特殊敵 時間経過でバフ
  :?foe = acronian_cultist
    ?foe.hp < 1000
      weapon._equip(6, false)
      activate R
    :?item.GetCooldown("mask") = 0
      weapon._equip(11, false)
      activate R
    // moon AAC
    :
      ?time%3 = 0
        weapon._equip(30, false)
      :?time%3 = 1
        weapon._equip(31, false)
      :?time%3 = 2
        weapon._equip(32, false)
  :
    // smite attack
    weapon._equip(25, wand_flip)

/** 神殿 **/
// ボス部屋判定可能
?loc = nagaraja
  ?foe.distance > 17
    move.action()
  :
    boss_attack()
:?loc = temple
  ?summon.count = 0 & time > 1
    follow.fire_talisman()
    // initialize
    move.set_shield("ice socketed_shield")
  :?foe.distance > 17
    move.action()
  :
    normal_attack()